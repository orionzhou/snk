import os
import os.path as op
from snk.utils import check_config, check_config_ase

configfile: 'config.yaml'
workdir: config['dirw']
config = check_config(config)
check_config_ase(config)

wildcard_constraints:
    sid = "[a-zA-Z0-9]+",
    region = ".+(:[0-9]+-[0-9]+)?"
def multiqc_inputs(wildcards):
    inputs = [
        "31_featurecounts/01.txt.summary",
    ]
    for sid in config['SampleID']:
        if config['t'][sid]['paired']:
            inputs.append("21_star/%s_p/Log.final.out" % sid)
            inputs.append("21_star/%s_u/Log.final.out" % sid)
            inputs.append("logs/trimmomatic_pe/%s.log" % sid)
        else:
            inputs.append("21_star/%s/Log.final.out" % sid)
            inputs.append("logs/trimmomatic_se/%s.log" % sid)
    return inputs

include: "rules/fq_rename.smk"
include: "rules/trimmomatic.smk"
include: "rules/star.smk"
include: "rules/gatk_haplotype_caller.smk"
include: "rules/featurecounts.smk"
include: "rules/multiqc.smk"
rule all:
    input:
        "qc/multiqc.html",
        expand(["22_bam/{sid}.bam"], sid = config['SampleID']),
        expand(["25_variants/{sid}.g.vcf.gz"], sid = config['SampleID']),
        #expand(["33_ase/{sid}.tsv"], sid = config['SampleID'])

for rule in workflow.rules:
    if rule.name != 'all':
        snakemake.utils.makedirs(op.join(config['dirp'], rule.name))

onsuccess:
    shell("mail -s 'Success: %s' %s < {log}" % (config['dirw'], config['email']))
onerror:
    shell("mail -s 'Error: %s' %s < {log}" % (config['dirw'], config['email']))
